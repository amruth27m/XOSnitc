alias physicalSP S0;
physicalSP = ([PTBR + 2 * (SP / 512)] * 512) + (SP % 512);

alias sysCallNo S1;
sysCallNo = [physicalSP - 1];

//Fork
if(sysCallNo==8) then
	alias ctr S2;
	ctr=0;
	while(ctr<32) do
		if([READY_LIST+ctr*32+1]==0) then
			break;
		endif;
		ctr=ctr+1;
	endwhile;
	//print ctr;

	if(ctr==32) then				//ctr is childPID
		[physicalSP-2]=-1;
		ireturn;
	endif;

	[READY_LIST+32*ctr]=ctr;

	alias curPID S3;
	curPID=(PTBR-1024)/8;

	alias pageCtr S4;
	pageCtr=0;

	alias iter S5;
	iter=0;

	while(iter<4) do
		if([PTBR+iter*2+1]=="01" || [PTBR+iter*2+1]=="11") then
			pageCtr=pageCtr+1;
		endif;
		iter=iter+1;
	endwhile;	

	//print iter;

	alias memctr S6;
	memctr=0;
	iter=29;

	while(iter < 64) do
		if([MEM_LIST+iter]==0) then
			memctr=memctr+1;
			//print memctr;
		endif;
		if(memctr==pageCtr) then
			break;
		endif;
		iter=iter+1;
	endwhile;

	//not enough memory
	if(memctr<pageCtr) then
		print "Memory Full";
		[physicalSP-2]=-1;
		ireturn;
	endif;

	alias counter S7;
	alias pageNo S8;
	alias cPTBR S9;
	counter=0;
	iter=29;
	cPTBR=ctr*8+1024;

	while(counter<4) do

		if([PTBR+counter*2+1]=="00") then
			[cPTBR+2*counter+1]="00";
			counter=counter+1;
			continue;
		endif;

		while(iter<64) do
			if([MEM_LIST+iter]==0) then
				[MEM_LIST+iter]=1;
				//print iter;
				break;
			endif;
			iter=iter+1;
		endwhile;

		memctr=0;
		pageNo=[PTBR+counter*2];
		//print pageNo;
		while(memctr<512) do
			[iter*512+memctr]=[pageNo*512+memctr];
			memctr=memctr+1;
		endwhile;

		[cPTBR+counter*2]=iter;
		[cPTBR+counter*2+1]="01";

		counter=counter+1;
	endwhile;

	[READY_LIST+ctr*32+2]=BP;
	[READY_LIST+ctr*32+3]=SP-1;

	//IP
	[READY_LIST+ctr*32+4]=[physicalSP];

	[READY_LIST+ctr*32+5]=cPTBR;
	[READY_LIST+ctr*32+6]=4;

	[READY_LIST+ctr*32+7]=R0;
	[READY_LIST+ctr*32+8]=R1;
	[READY_LIST+ctr*32+9]=R2;
	[READY_LIST+ctr*32+10]=R3;
	[READY_LIST+ctr*32+11]=R4;
	[READY_LIST+ctr*32+12]=R5;
	[READY_LIST+ctr*32+13]=R6;
	[READY_LIST+ctr*32+14]=R7;

	[READY_LIST+ctr*32+31]=curPID;

	counter=0;
	while(counter<8) do
		[READY_LIST+ctr*32+15+counter*2]=[READY_LIST+curPID*32+15+counter*2];
		[READY_LIST+ctr*32+15+counter*2+1]=[READY_LIST+curPID*32+15+counter*2+1];
		if([READY_LIST+ctr*32+15+counter*2]!=-1) then
			[FILE_TABLE+2*[READY_LIST+ctr*32+15+counter*2]+1]=[FILE_TABLE+2*[READY_LIST+ctr*32+15+counter*2]+1]+1;
		endif;
		counter=counter+1;
	endwhile;

	[READY_LIST+ctr*32+1]=1;
	[physicalSP-2]=ctr;

	alias cphysicalSP S10;
	cphysicalSP=([cPTBR + 2 * ((SP-1) / 512)] * 512) + ((SP-1) % 512);
	[cphysicalSP-1]=-2;
	ireturn;

endif;